plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = uri('https://oss.sonatype.org/content/groups/public/')
    }
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.8.1'
    implementation 'com.google.inject:guice:4.2.3'
    implementation 'com.google.code.gson:gson:2.2.4'
    implementation 'net.kyori:adventure-api:4.0.0-SNAPSHOT'
    implementation 'net.kyori:adventure-text-serializer-gson:4.0.0-SNAPSHOT'
    implementation 'org.yaml:snakeyaml:1.21'
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'

    testImplementation 'org.loomdev:minecraft-server:1.16.1-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.4.2'

    compileOnly 'org.jetbrains:annotations:19.0.0'
}

group = 'org.loomdev'
version = '1.16.1-SNAPSHOT'
description = 'Loom API'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

sourceSets {
    ap {
        compileClasspath += main.compileClasspath + main.output
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
    from sourceSets.ap.output
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

jar {
    from sourceSets.ap.output
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.source = '8'
    options.links(
            'http://www.slf4j.org/apidocs/'
    )

    // Disable the crazy super-strict doclint tool in Java 8
    // options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
